apply plugin: 'application'
apply plugin: 'idea'

repositories {
    jcenter()
}

configurations {
    checkstyleClasspath
}

mainClassName = "io.dronecore.example.TakeoffAndLand"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

task takeoffAndLand(dependsOn: 'classes', type: JavaExec) {
    main = "io.dronecore.example.TakeoffAndLand"
    classpath = sourceSets.main.runtimeClasspath
}

task setRtlAltitude(dependsOn: 'classes', type: JavaExec) {
    main = "io.dronecore.example.SetRtlAltitude"
    classpath = sourceSets.main.runtimeClasspath
}

task checkstyle(type: Checkstyle) {
    configFile = rootProject.file("config/checkstyle/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = project.files()
    checkstyleClasspath = project.configurations.checkstyleClasspath

    maxWarnings 0

    reports {
        xml.enabled true
        xml.destination new File("$project.buildDir/reports/checkstyle/checkstyle_report.xml")

        html.enabled true
        html.destination new File("$project.buildDir/reports/checkstyle/checkstyle_report.html")
    }
}

build.dependsOn 'checkstyle'

idea {
    module {
        inheritOutputDirs = false
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

dependencies {
    checkstyleClasspath 'com.puppycrawl.tools:checkstyle:8.5'

    implementation 'org.dronecode.sdk:dronecode-sdk:0.1.0'

    implementation 'ch.qos.logback:logback-core:1.2.3'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'org.slf4j:slf4j-api:1.7.25'
}
